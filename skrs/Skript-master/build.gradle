import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.apache.tools.ant.filters.ReplaceTokens

import java.time.LocalTime

plugins {
	id 'com.gradleup.shadow' version '9.2.2'
	id 'maven-publish'
	id 'java'
	id 'checkstyle'
}

configurations {
    testImplementation.extendsFrom testShadow
}

allprojects {
	repositories {
		mavenCentral()
		maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
		maven { url 'https://repo.papermc.io/repository/maven-public/' }
		maven { url 'https://ci.emc.gs/nexus/content/groups/aikar/' }
	}
}

dependencies {
	shadow group: 'io.papermc', name: 'paperlib', version: '1.0.8'
	shadow group: 'org.bstats', name: 'bstats-bukkit', version: '3.1.0'
	shadow group: 'net.kyori', name: 'adventure-text-serializer-bungeecord', version: '4.4.1'

	implementation group: 'io.papermc.paper', name: 'paper-api', version: '1.21.11-R0.1-SNAPSHOT'
	implementation group: 'com.google.code.findbugs', name: 'findbugs', version: '3.0.1'

	// bundled with Minecraft 1.19.4+ for display entity transforms
	implementation group: 'org.joml', name: 'joml', version: '1.10.8'

	// Plugin hook libraries
	implementation group: 'com.sk89q.worldguard', name: 'worldguard-legacy', version: '7.0.0-SNAPSHOT', {
		exclude group: 'org.bukkit', module: 'bukkit'
	}
	implementation group: 'net.milkbowl.vault', name: 'Vault', version: '1.7.3', {
		exclude group: 'org.bstats', module: 'bstats-bukkit'
		exclude group: 'org.bukkit', module: 'bukkit'
	}

	implementation fileTree(dir: 'lib', include: '*.jar')

	testShadow group: 'junit', name: 'junit', version: '4.13.2'
	testShadow group: 'org.easymock', name: 'easymock', version: '5.5.0'
}

checkstyle {
	configFile = new File("checkstyle.xml")
	sourceSets = [] // disables checkstyle after build task
}

tasks.register('checkAliases') {
	description 'Checks for the existence of the aliases.'
	doLast {
		def aliasFolder = project.file('skript-aliases')
		if (!aliasFolder.exists() || aliasFolder.listFiles().length == 0) {
			throw new InvalidUserDataException('Aliases are missing from \'skript-aliases\' folder. Consider fetching submodules with \'git submodule update --init\'.')
		}
	}
}

tasks.register('testJar', ShadowJar) {
	dependsOn(compileTestJava)
	archiveFileName = 'Skript-JUnit.jar'
	from sourceSets.test.output, sourceSets.main.output
	from {
		project.configurations.testShadow.collect { it.isDirectory() ? it : zipTree(it) }
	}
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task jar(overwrite: true, type: ShadowJar) {
	dependsOn checkAliases
	archiveFileName = jarName ? 'Skript-' + project.version + '.jar' : jarName
	from sourceSets.main.output
}

task build(overwrite: true, type: ShadowJar) {
	archiveFileName = jarName ? 'Skript-' + project.version + '.jar' : jarName
	from sourceSets.main.output
}

// Excludes the tests for the build task. Should be using JUnitQuick, JUnitJava21, skriptTest, quickTest.
// We do not want tests to run for building. That's time consuming and annoying. Especially in development.
test {
	exclude '**/*'
}

tasks.register('sourceJar', Jar) {
	from sourceSets.main.allJava
	archiveClassifier = 'sources'
}

tasks.withType(ShadowJar).configureEach {
	configurations = [
		project.configurations.shadow
	]
	dependencies {
		include(dependency('io.papermc:paperlib'))
		include(dependency('org.bstats:bstats-bukkit'))
		include(dependency('org.bstats:bstats-base'))
		include(dependency('net.kyori:adventure-text-serializer-bungeecord'))
	}
	relocate 'io.papermc.lib', 'ch.njol.skript.paperlib'
	relocate 'org.bstats', 'ch.njol.skript.bstats'
	manifest {
		attributes(
			'Name': 'ch/njol/skript',
			'Automatic-Module-Name': 'ch.njol.skript',
			'Sealed': 'true'
		)
	}
	from 'skript-aliases', {
		into 'aliases-english' // Change this if we get aliases in other languages
	}
}

processResources {
	filter ReplaceTokens, tokens: [
		'version'         : project.property('version'),
		'today'           : 'unknown', // Don't leak information about system this was built on
		'release-flavor'  : 'selfbuilt-unknown', // Note: 'selfbuilt' prefix makes updater report a custom build
		'release-channel' : 'none', // No updates, so anything else wouldn't make sense
		'release-updater' : 'ch.njol.skript.update.NoUpdateChecker', // Disable update checking
		'release-source'  : '',
		'release-download': 'null'
	]
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId 'com.github.SkriptLang'
			artifactId 'Skript'
			version project.version
			artifact sourceJar
			artifact tasks.jar
		}
	}

	repositories {
		maven {
			name = 'repo'
			url = 'https://repo.skriptlang.org/releases'
			credentials {
				username = System.getenv('MAVEN_USERNAME')
				password = System.getenv('MAVEN_PASSWORD')
			}
		}
	}
}

// Task to check that test scripts are named correctly
tasks.register('testNaming') {
	doLast {
		// Regression tests
		for (def file : project.file('src/test/skript/tests/regressions').listFiles()) {
			def name = file.getName()
			if (name.toLowerCase(Locale.ENGLISH) != name) {
				throw new InvalidUserDataException('invalid test name: ' + name)
			}
		}

		// Syntax tests
		for (def dir : project.file('src/test/skript/tests/syntaxes').listFiles()) {
			for (def file : dir.listFiles()) {
				def name = file.getName()
				if (!name.startsWith('.') && !Character.isJavaIdentifierStart(name.codePointAt(0))) {
					throw new InvalidUserDataException('invalid test name: ' + name)
				}
			}
		}
	}
}

enum Modifiers {
	DEV_MODE, GEN_NIGHTLY_DOCS, GEN_RELEASE_DOCS, DEBUG, PROFILE, JUNIT
}

// Create a test task with given name, environments dir/file, dev mode and java version.
// -1 on the timeout means it'll be disabled.
void createTestTask(String name, String desc, String environments, int javaVersion, long timeout, Modifiers... modifiers) {
	if (timeout == 0)
		timeout = 300000 // 5 minutes
	boolean junit = modifiers.contains(Modifiers.JUNIT)
	boolean releaseDocs = modifiers.contains(Modifiers.GEN_RELEASE_DOCS)
	boolean docs = modifiers.contains(Modifiers.GEN_NIGHTLY_DOCS) || releaseDocs
	def artifact = 'build' + File.separator + 'libs' + File.separator
	if (junit) {
		artifact += 'Skript-JUnit.jar'
	} else if (releaseDocs) {
		artifact += 'Skript-' + version + '.jar'
	} else {
		artifact += 'Skript-nightly.jar'
	}
	tasks.register(name, JavaExec) {
		description = desc
		if (junit) {
			dependsOn testJar
		} else if (releaseDocs) {
			dependsOn githubRelease, testNaming
		} else {
			dependsOn nightlyRelease, testNaming
		}
		javaLauncher = javaToolchains.launcherFor {
	        languageVersion = JavaLanguageVersion.of(javaVersion)
	    }
		if (modifiers.contains(Modifiers.DEV_MODE)) {
			standardInput = System.in
		}
		group = 'execution'
		classpath = files([
			artifact,
			project.configurations.runtimeClasspath.find { it.name.startsWith('gson') },
			sourceSets.main.runtimeClasspath
		])
		getMainClass().set('ch.njol.skript.test.platform.PlatformMain')
		args = [
			'build/test_runners',
			junit ? 'src/test/skript/junit' : 'src/test/skript/tests',
			'src/test/resources/runner_data',
			environments,
			modifiers.contains(Modifiers.DEV_MODE),
			docs,
			junit,
			modifiers.contains(Modifiers.DEBUG),
			project.findProperty('verbosity') ?: "null",
			timeout
		]

		// Do first is used when throwing exceptions.
		// This way it's not called when defining the task.
		doFirst {
			if (!gradle.taskGraph.hasTask(":tasks") && !gradle.startParameter.dryRun && modifiers.contains(Modifiers.PROFILE)) {
				if (!project.hasProperty('profiler'))
					throw new MissingPropertyException('Add parameter -Pprofiler=<path to profiler>', 'profiler', String.class)

				args += '-agentpath:' + project.property('profiler') + '=port=8849,nowait'
			}
		}
	}
}

def java21 = 21

def env = project.testEnv + ".json"
def latestJava = java21
def oldestJava = java21

def latestJUnitEnv = env
def latestJUnitJava = latestJava

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(latestJava))
	sourceCompatibility = oldestJava
	targetCompatibility = oldestJava
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

// Register different Skript testing tasks
String environments = 'src/test/skript/environments/';
int envJava = project.property('testEnvJavaVersion') == null ? latestJava : Integer.parseInt(project.property('testEnvJavaVersion') as String)
createTestTask('quickTest', 'Runs tests on one environment being the latest supported Java and Minecraft.', environments + env, latestJava, 0)
createTestTask('skriptTestJava21', 'Runs tests on all Java 21 environments.', environments + 'java21', java21, 0)
createTestTask('skriptTestDev', 'Runs testing server and uses \'system.in\' for command input, stop server to finish.', environments + env, envJava, 0, Modifiers.DEV_MODE, Modifiers.DEBUG)
createTestTask('skriptProfile', 'Starts the testing server with JProfiler support.', environments + env, latestJava, -1, Modifiers.PROFILE)
createTestTask('genNightlyDocs', 'Generates the Skript documentation website html files.', environments + env, envJava, 0, Modifiers.GEN_NIGHTLY_DOCS)
createTestTask('genReleaseDocs', 'Generates the Skript documentation website html files for a release.', environments + env, envJava, 0, Modifiers.GEN_RELEASE_DOCS)
tasks.register('skriptTest') {
	description = 'Runs tests on all environments.'
	dependsOn skriptTestJava21
}

createTestTask('JUnitQuick', 'Runs JUnit tests on one environment being the latest supported Java and Minecraft.', environments + latestJUnitEnv, latestJUnitJava, 0, Modifiers.JUNIT)
createTestTask('JUnitJava21', 'Runs JUnit tests on all Java 21 environments.', environments + 'java21', java21, 0, Modifiers.JUNIT)
tasks.register('JUnit') {
	description = 'Runs JUnit tests on all environments.'
	dependsOn JUnitJava21
}

// custom test task
// usage: gradle customTest -PtestEnvs=<environment files, comma separated list.> -PtestEnvJavaVersion=<java version> -Ptimeout=<timeout in ms> -Pjunit=[true|false]
// defaults: testEnvJavaVersion=latestJava, timeout=0, junit=false
// example: gradle customTest -PtestEnvs="java21/paper-1.21.4,java21/paper-1.21.8" -PtestEnvJavaVersion=21 -Ptimeout=600000 -Pjunit=true

// get environments
String propEnvs = project.hasProperty('testEnvs') ? project.property('testEnvs') as String : project.testEnv
String[] envList = propEnvs != null ? propEnvs.split(',') : [env]
StringBuilder customEnvironmentsBuilder = new StringBuilder()
for (int i = 0; i < envList.length; i++) {
	customEnvironmentsBuilder.append(environments).append(envList[i]).append('.json')
	if (i < envList.length - 1) {
		customEnvironmentsBuilder.append(',') // use ',' as separator for multiple environments
	}
}
String customEnvironments = customEnvironmentsBuilder.toString()

long customTimeout = project.hasProperty('timeout') ? Long.parseLong(project.property('timeout') as String) : 0
List<Modifiers> customModifiers = new ArrayList<>()
if (project.hasProperty('junit') && (project.property('junit') as String).toLowerCase() == 'true') {
	customModifiers.add(Modifiers.JUNIT)
}
createTestTask('customTest', 'Runs tests based on provided parameters.', customEnvironments, envJava, customTimeout, customModifiers.toArray(new Modifiers[0]) as Modifiers[])
// end custom test task

// Build flavor configurations
tasks.register('githubResources', ProcessResources) {
	from 'src/main/resources', {
		include '**'
		version = project.property('version')
		def channel = 'stable'
		if (version.contains('-'))
			channel = 'prerelease'
		filter ReplaceTokens, tokens: [
			'version'         : version,
			'today'           : '' + LocalTime.now(),
			'release-flavor'  : 'skriptlang-github', // SkriptLang build, distributed on Github
			'release-channel' : channel, // Release channel, see above
			'release-updater' : 'ch.njol.skript.update.GithubChecker', // Github API client
			'release-source'  : 'https://api.github.com/repos/SkriptLang/Skript/releases', // Github API URL
			'release-download': 'null' // Update checker will figure it out
		]
	}
	into 'build/resources/main'
}

tasks.register('githubRelease', ShadowJar) {
	from sourceSets.main.output
	dependsOn githubResources
	archiveFileName = 'Skript-' + version + '.jar'
	manifest {
		attributes(
			'Name': 'ch/njol/skript',
			'Automatic-Module-Name': 'ch.njol.skript',
			'Sealed': 'true'
		)
	}
}

tasks.register('spigotResources', ProcessResources) {
	from 'src/main/resources', {
		include '**'
		version = project.property('version')
		def channel = 'stable'
		if (version.contains('-'))
			channel = 'prerelease'
		filter ReplaceTokens, tokens: [
			'version'         : version,
			'today'           : '' + LocalTime.now(),
			'release-flavor'  : 'skriptlang-spigot', // SkriptLang build, distributed on Spigot resources
			'release-channel' : channel, // Release channel, see above
			'release-updater' : 'ch.njol.skript.update.GithubChecker', // Github API client
			'release-source'  : 'https://api.github.com/repos/SkriptLang/Skript/releases', // Github API URL
			'release-download': '\'http://example.com\'' // TODO insert Spigot URL
		]
	}
	into 'build/resources/main'
}

tasks.register('spigotRelease', ShadowJar) {
	from sourceSets.main.output
	dependsOn spigotResources
	archiveFileName = 'Skript-spigot.jar'
	manifest {
		attributes(
			'Name': 'ch/njol/skript',
			'Automatic-Module-Name': 'ch.njol.skript',
			'Sealed': 'true'
		)
	}
}

tasks.register('nightlyResources', ProcessResources) {
	from 'src/main/resources', {
		include '**'
		def hash = 'git rev-parse --short HEAD'.execute().text.trim()
		version = project.property('version') + '-nightly-' + hash
		filter ReplaceTokens, tokens: [
			'version'         : version,
			'today'           : '' + LocalTime.now(),
			'release-flavor'  : 'skriptlang-nightly', // SkriptLang build, automatically done by CI
			'release-channel' : 'prerelease', // No update checking, but these are VERY unstable
			'release-updater' : 'ch.njol.skript.update.NoUpdateChecker', // No auto updates for now
			'release-source'  : '',
			'release-download': 'null'
		]
	}
	into 'build/resources/main'
}

tasks.register('nightlyRelease', ShadowJar) {
	from sourceSets.main.output
	dependsOn nightlyResources
	archiveFileName = 'Skript-nightly.jar'
	manifest {
		attributes(
			'Name': 'ch/njol/skript',
			'Automatic-Module-Name': 'ch.njol.skript',
			'Sealed': 'true'
		)
	}
}

javadoc {
	mustRunAfter(tasks.withType(ProcessResources))
	title = 'Skript ' + project.property('version')
	source = sourceSets.main.allJava

	exclude("ch/njol/skript/conditions/**")
	exclude("ch/njol/skript/expressions/**")
	exclude("ch/njol/skript/effects/**")
	exclude("ch/njol/skript/events/**")
	exclude("ch/njol/skript/sections/**")
	exclude("ch/njol/skript/structures/**")
	exclude("ch/njol/skript/lang/function/EffFunctionCall.java")
	exclude("ch/njol/skript/lang/function/ExprFunctionCall.java")
	exclude("ch/njol/skript/hooks/**")
	exclude("ch/njol/skript/test/**")

	classpath = configurations.compileClasspath + sourceSets.main.output
	options.encoding = 'UTF-8'
	// currently our javadoc has a lot of errors, so we need to suppress the linter
	options.addStringOption('Xdoclint:none', '-quiet')
}
