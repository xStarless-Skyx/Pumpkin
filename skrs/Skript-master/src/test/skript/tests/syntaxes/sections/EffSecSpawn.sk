test "spawn section":
	set {_before} to 5
	spawn a pig at test-location:

		# Make sure variables carry over properly
		assert {_before} is 5 with "value of {_before} should be 5 (got '%{_before}%')"
		add 5 to {_before}
		set {_new var} to 5

		assert event-entity is a pig with "entity not a pig"
		set {_test} to event-entity
		assert {_test} is set with "entity not set"
		assert {_test} is a pig with "entity variable not a pig"
		set {_location} to test-location
		assert {_location} is set with "location not set"
		assert y-coord of test-location is y-coord of {_location} with "location y coordinate not right"

	delete the last spawned pig

	# Make sure variables carry over properly
	assert {_before} is 10 with "value of {_before} should be 10 (got '%{_before}%')"
	assert {_new var} is 5 with "value of {_new var} should be 5 (got '%{_new var}%')"

test "spawn cats by type":
	delete all cats
	set {_l} to location of spawn of world "world"
	spawn 5 all black cats at {_l}
	assert size of all cats = 5 with "Size of all cats is not 5"
	assert size of all all black cats = 5 with "Size of all all black cats is not 5"
	spawn 2 siamese cats at {_l}
	assert size of all cats = 7 with "Size of all cats is not 7"
	assert size of all siamese cats = 2 with "Size of all siamese cats is not 2"
	delete all siamese cats
	assert size of all cats = 5 with "Size of all cats is not 5 after delete 2 siamese cats"
	delete all cats
	assert size of all cats = 0 with "Size of all cats is greater than 0 after all were deleted"

test "spawn wolves by variant" when running minecraft "1.21.0":
	delete all wolves
	set {_l} to location of spawn of world "world"
	spawn 5 ashen wolves at {_l}
	assert size of all wolves = 5 with "Size of all wolves is not 5"
	assert size of all ashen wolves = 5 with "Size of all ashen wolves is not 5"
	spawn 2 rusty wolves at {_l}
	assert size of all wolves = 7 with "Size of all wolves is not 7"
	assert size of all rusty wolves = 2 with "Size of all rusty wolves is not 2"
	delete all rusty wolves
	assert size of all wolves = 5 with "Size of all wolves is not 5 after delete 2 rusty wolves"
	delete all wolves
	assert size of all wolves = 0 with "Size of all wolves is greater than 0 after all were deleted"

test "spawn salmon by variant" when running minecraft "1.21.2":
	delete all salmons
	set {_l} to test-location
	spawn 5 small salmon at {_l}
	assert size of all small salmons = 5 with "Size of all small salmons is not 5"
	assert size of all entities of type salmon = 5 with "Size of all salmons is not 5"
	spawn 3 medium salmon at {_l}
	assert size of all medium salmons = 3 with "Size of all medium salmons is not 3"
	assert size of all entities of type salmon = 8 with "Size of all salmons is not 8"
	spawn 2 large salmon at {_l}
	assert size of all large salmons = 2 with "Size of all large salmon is not 2"
	assert size of all entities of type salmon = 10 with "Size of all salmon is not 10"
	delete all large salmons
	assert size of all large salmons = 0 with "Large salmons did not get cleared"
	delete all medium salmons
	assert size of all medium salmons = 0 with "Medium salmons did not get cleared"
	delete all small salmons
	assert size of all small salmons = 0 with "Small salmons did not get cleared"
	spawn 15 of any salmon at {_l}
	assert size of all entities of type salmon = 15 with "Size of all salmons is not 15"
	clear all salmons
	assert size of all entities of type salmon = 0 with "All salmons did not get cleared"

test "spawn entities":
	set {_entities::*} to "allay", "axolotl", "bat", "bee", "blaze", "cat", "cave spider", "chicken" and "cod"
	add "cow", "creeper", "dolphin", "donkey", "drowned", "elder guardian", "enderman" and "endermite" to {_entities::*}
	add "evoker", "fox", "frog", "ghast", "glow squid", "goat", "guardian", "hoglin", "horse" and "husk" to {_entities::*}
	add "iron golem", "llama", "magma cube", "mooshroom", "mule", "ocelot", "panda", "parrot" and "phantom" to {_entities::*}
	add "pig", "piglin", "piglin brute", "pillager", "polar bear", "pufferfish", "rabbit" and "ravager" to {_entities::*}
	add "salmon", "sheep", "shulker", "silverfish", "skeleton", "skeleton horse" and "slime" to {_entities::*}
	add "snow golem", "spider", "squid", "stray", "strider", "tadpole", "trader llama" and "tropical fish" to {_entities::*}
	add "turtle", "vex", "villager", "vindicator", "wandering trader", "warden", "witch" and "wither skeleton" to {_entities::*}
	add "wolf", "zoglin", "zombie", "zombie horse", "zombie villager", and "zombified piglin" to {_entities::*}
	add "ender dragon" and "wither" to {_entities::*}
	add "oak boat", "oak chest boat", "armor stand", "primed tnt", "firework", "arrow", "trident" and "egg" to {_entities::*}
	add "snowball", "end crystal", "eye of ender", "regular minecart", "falling sand" and "falling gravel" to {_entities::*}
	add "falling powder snow", "fireball" and "wither skull" to {_entities::*}

	if running minecraft "1.20.0":
		add "camel" and "sniffer" to {_entities::*}
	if running minecraft "1.20.6":
		add "armadillo" to {_entities::*}
	if running minecraft "1.21.0":
		add "bogged", "breeze" and "wind charge" to {_entities::*}
	if running minecraft "1.21.6":
		add "happy ghast" and "ghastling" to {_entities::*}
	if running minecraft "1.21.9":
		add "copper golem" to {_entities::*}

	# Note: Had to remove 'fireball' and 'wind charge'

	clear all entities
	loop {_entities::*}:
		set {_parse} to loop-value parsed as entity type
		if {_parse} is set:
			spawn {_parse} at spawn of "world":
				set {_ent} to entity
			set {_assert} to whether {_ent} is entity type within {_parse}
			assert {_assert} is true with "", expected (entity type within {_parse}), got {_ent}
			clear entity within {_ent}
			clear {_ent}
		else:
			assert false is true with "Failed to parse '%loop-value%' as an entity type", expected loop-value, got {_parse}
	clear all entities

test "spawn villagers by profession":
	clear all entities
	spawn 2 unemployed villagers at test-location
	assert size of all unemployed villagers is 2 with "Should be 2 unemployed villagers"
	assert size of all villagers is 2 with "Should be 2 total villagers"
	spawn 2 armorers at test-location
	assert size of all armorers is 2 with "Should be 2 armorer villagers"
	assert size of all villagers is 4 with "Should be 4 total villagers"
	spawn 2 butchers at test-location
	assert size of all butchers is 2 with "Should be 2 butcher villagers"
	assert size of all villagers is 6 with "Should be 6 total villagers"
	delete all butchers
	assert size of all butchers is 0 with "Should be 0 butcher villagers"
	assert size of all villagers is 4 with "Should be 4 total villagers"
	delete all armorers
	assert size of all armorers is 0 with "Should be 0 armorer villagers"
	assert size of all villagers is 2 with "Should be 2 total villagers"
	delete all unemployed villagers
	assert size of all unemployed villagers is 0 with "Should be 0 unemployed villagers"
	assert size of all villagers is 0 with "Should be 0 total villagers"
	spawn 10 villagers at test-location
	assert size of all villagers is 10 with "Should be 10 villagers"
	delete all villagers
	assert size of all villagers is 0 with "Should be 0 villagers"

test "spawn zombie villagers by profession":
	clear all entities
	spawn 2 unemployed zombie villagers at test-location
	assert size of all unemployed zombie villagers is 2 with "Should be 2 unemployed zombie villagers"
	assert size of all zombie villagers is 2 with "Should be 2 total zombie villagers"
	spawn 2 zombie armorers at test-location
	assert size of all zombie armorers is 2 with "Should be 2 zombie armorer villagers"
	assert size of all zombie villagers is 4 with "Should be 4 total zombie villagers"
	spawn 2 zombie butchers at test-location
	assert size of all zombie butchers is 2 with "Should be 2 zombie butcher villagers"
	assert size of all zombie villagers is 6 with "Should be 6 total zombie villagers"
	delete all zombie butchers
	assert size of all zombie butchers is 0 with "Should be 0 zombie butcher villagers"
	assert size of all zombie villagers is 4 with "Should be 4 zombie total villagers"
	delete all zombie armorers
	assert size of all zombie armorers is 0 with "Should be 0 zombie armorer villagers"
	assert size of all zombie villagers is 2 with "Should be 2 total zombie villagers"
	delete all unemployed zombie villagers
	assert size of all unemployed villagers is 0 with "Should be 0 zombie unemployed villagers"
	assert size of all zombie villagers is 0 with "Should be 0 total zombie villagers"
	spawn 10 zombie villagers at test-location
	assert size of all zombie villagers is 10 with "Should be 10 zombie villagers"
	delete all zombie villagers
	assert size of all zombie villagers is 0 with "Should be 0 zombie villagers"

test "spawn pig by variant" when running minecraft "1.21.5":
	delete all pigs
	set {_l} to test-location
	spawn 5 warm pigs at {_l}
	assert size of all warm pigs is 5 with "Size of all warm pigs is not 5"
	assert size of all pigs is 5 with "Size of all pigs is not 5"
	spawn 3 temperate pigs at {_l}
	assert size of all temperate pigs is 3 with "Size of all temperate pigs is not 3"
	assert size of all pigs is 8 with "Size of all pigs is not 8"
	spawn 2 cold pigs at {_l}
	assert size of all cold pigs is 2 with "Size of all cold pigs is not 2"
	assert size of all pigs is 10 with "Size of all pigs is not 10"
	delete all cold pigs
	assert size of all cold pigs is 0 with "Cold pigs were not deleted"
	assert size of all pigs is 8 with "Only cold pigs should have been deleted"
	delete all temperate pigs
	assert size of all temperate pigs is 0 with "Temperate pigs were not deleted"
	assert size of all pigs is 5 with "Only temperate pigs should have been deleted"
	delete all warm pigs
	assert size of all warm pigs is 0 with "Warm pigs were not deleted"

test "spawn chicken by variant" when running minecraft "1.21.5":
	delete all chickens
	set {_l} to test-location
	spawn 5 warm chickens at {_l}
	assert size of all warm chickens is 5 with "Size of all warm chickens is not 5"
	assert size of all chickens (entity type) is 5 with "Size of all chickens is not 5"
	spawn 3 temperate chickens at {_l}
	assert size of all temperate chickens is 3 with "Size of all temperate chickens is not 3"
	assert size of all chickens (entity type) is 8 with "Size of all chickens is not 8"
	spawn 2 cold chickens at {_l}
	assert size of all cold chickens is 2 with "Size of all cold chickens is not 2"
	assert size of all chickens (entity type) is 10 with "Size of all chickens is not 10"
	delete all cold chickens
	assert size of all cold chickens is 0 with "Cold chickens were not deleted"
	assert size of all chickens (entity type) is 8 with "Only cold chickens should have been deleted"
	delete all temperate chickens
	assert size of all temperate chickens is 0 with "Temperate chickens were not deleted"
	assert size of all chickens (entity type) is 5 with "Only temperate chickens should have been deleted"
	delete all warm chickens
	assert size of all warm chickens is 0 with "Warm chickens were not deleted"

test "spawn baby chicken by variant" when running minecraft "1.21.5":
	delete all chicks
	set {_l} to test-location
	spawn 5 warm chicks at {_l}
	assert size of all warm chicks is 5 with "Size of all warm chicks is not 5"
	assert size of all chicks is 5 with "Size of all chicks is not 5"
	spawn 3 temperate chicks at {_l}
	assert size of all temperate chicks is 3 with "Size of all temperate chicks is not 3"
	assert size of all chicks is 8 with "Size of all chicks is not 8"
	spawn 2 cold chicks at {_l}
	assert size of all cold chicks is 2 with "Size of all cold chicks is not 2"
	assert size of all chicks is 10 with "Size of all chicks is not 10"
	delete all cold chicks
	assert size of all cold chicks is 0 with "Cold chicks were not deleted"
	assert size of all chicks is 8 with "Only cold chicks should have been deleted"
	delete all temperate chicks
	assert size of all temperate chicks is 0 with "Temperate chicks were not deleted"
	assert size of all chicks is 5 with "Only temperate chicks should have been deleted"
	delete all warm chicks
	assert size of all warm chicks is 0 with "Warm chicks were not deleted"

test "spawn cow by variant" when running minecraft "1.21.5":
	delete all cows
	set {_l} to test-location
	spawn 5 warm cows at {_l}
	assert size of all warm cows is 5 with "Size of all warm cows is not 5"
	assert size of all cows is 5 with "Size of all cows is not 5"
	spawn 3 temperate cows at {_l}
	assert size of all temperate cows is 3 with "Size of all temperate cows is not 3"
	assert size of all cows is 8 with "Size of all cows is not 8"
	spawn 2 cold cows at {_l}
	assert size of all cold cows is 2 with "Size of all cold cows is not 2"
	assert size of all cows is 10 with "Size of all cows is not 10"
	delete all cold cows
	assert size of all cold cows is 0 with "Cold cows were not deleted"
	assert size of all cows is 8 with "Only cold cows should have been deleted"
	delete all temperate cows
	assert size of all temperate cows is 0 with "Temperate cows were not deleted"
	assert size of all cows is 5 with "Only temperate cows should have been deleted"
	delete all warm cows
	assert size of all warm cows is 0 with "Warm cows were not deleted"

test "spawn baby cow by variant" when running minecraft "1.21.5":
	delete all calves
	set {_l} to test-location
	spawn 5 warm calves at {_l}
	assert size of all warm calves is 5 with "Size of all warm calves is not 5"
	assert size of all calves is 5 with "Size of all calves is not 5"
	spawn 3 temperate calves at {_l}
	assert size of all temperate calves is 3 with "Size of all temperate calves is not 3"
	assert size of all calves is 8 with "Size of all calves is not 8"
	spawn 2 cold calves at {_l}
	assert size of all cold calves is 2 with "Size of all cold calves is not 2"
	assert size of all calves is 10 with "Size of all calves is not 10"
	delete all cold calves
	assert size of all cold calves is 0 with "Cold calves were not deleted"
	assert size of all calves is 8 with "Only cold calves should have been deleted"
	delete all temperate calves
	assert size of all temperate calves is 0 with "Temperate calves were not deleted"
	assert size of all calves is 5 with "Only temperate calves should have been deleted"
	delete all warm calves
	assert size of all warm calves is 0 with "Warm calves were not deleted"
