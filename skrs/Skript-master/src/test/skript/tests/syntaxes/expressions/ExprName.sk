test "name of world":
	set {_thing} to the world "world"
	assert name of {_thing} is "world" with "name was wrong"
	set the name of {_thing} to "blob"
	assert name of {_thing} is "world" with "world name changed"

	# silly test for plural any-named
	set {_things::*} to the world "world", the world "world" and the world "world"

	set {_blah::*} to the names of {_things::*}
	assert {_blah::*} is "world", "world" and "world" with "names were wrong"
	delete {_blah::*}
	set {_blah::*} to the name of {_things::*}
	assert {_blah::*} is "world", "world" and "world" with "names were wrong"

test "name of entity":
	set {_before} to 5
	spawn a pig at spawn of "world":
		assert event-entity is a pig with "entity not a pig"
		set {_test} to event-entity
		set event-entity's name to "foo"
		assert {_test} is set with "entity not set"
		assert {_test} is a pig with "entity variable not a pig"
		assert event-entity's name is "foo" with "name didn't change"
	assert {_test} exists with "entity didn't carry out"
	assert {_test}'s name is "foo" with "name didn't carry out"
	set {_test}'s name to "bar"
	assert {_test}'s name is "bar" with "name didn't change"
	delete the last spawned pig

test "name of item":
	set {_thing} to 3 of stone
	assert name of {_thing} does not exist with "name was set"
	set the name of {_thing} to "blob"
	assert name of {_thing} is "blob" with "item name didn't change"

test "name of block":
	set {_data} to blockdata of block at event-location
	set block at event-location to a chest
	assert name of block at event-location is not set with "The block shouldn't have a name yet"
	set name of block at event-location to "Mr Chesty"
	assert name of block at event-location = "Mr Chesty" with "The block should have a name now"
	reset name of block at event-location
	assert name of block at event-location is not set with "The block should no longer have a name"
	set block at event-location to {_data}

using script reflection

test "config name (new)":
	set {_node} to the skript config
	set {_node} to node "language" of {_node}
	assert name of {_node} is "language" with "config node name was wrong"

	# node in node
	set {_node} to the skript config
	set {_node} to node "disable hooks.vault" of {_node}
	assert name of {_node} is "vault" with "config node name was wrong"
	assert "%{_node}%" is "disable hooks.vault" with "config node path was wrong"

test "node name (new)":
	set {_node} to the current script
	set {_node} to the first element of nodes of {_node}
	assert name of {_node} is "test ""name of world""" with "first node name was wrong"

	set {_node} to the current script
	set {_node} to the 5th element of nodes of {_node} # Obviously, this changes if this file changes
	assert name of {_node} is "using script reflection" with "4th node name was wrong"

	# root node
	set {_node} to the skript config
	set {_node} to node "language" of {_node}
	assert name of {_node} is "language" with "config node name was wrong"

	# node in node
	set {_node} to the skript config
	set {_node} to node "disable hooks.vault" of {_node}
	assert name of {_node} is "vault" with "config node name was wrong"
	assert "%{_node}%" is "disable hooks.vault" with "config node path was wrong"

test "script name (new)":
	set {_name} to the script's name
	assert {_name} is "ExprName" with "script name is wrong"
	# New behaviour includes just the name, since path is available by string-ifying the script

test "function name (new)":
	set {_function} to the function named "testExprName"
	set {_name} to {_function}'s name
	assert {_name} is "testExprName" with "function name is wrong"

	# we want to make sure any brackets don't somehow get included
	set {_function} to the function named "testExprName()"
	set {_name} to {_function}'s name
	assert {_name} is "testExprName" with "function name is wrong"

local function testExprName():
	# do nothing
	stop
