using error catching

test "worldborder damage amount":
	set {_border} to worldborder of world("world")
	assert worldborder damage amount of {_border} is 0.2 with "Default worldborder damage amount is not 0.2"

	# set tests
	set worldborder damage amount of {_border} to 1
	assert worldborder damage amount of {_border} is 1 with "Failed to set worldborder damage amount to an integer"
	set worldborder damage amount of {_border} to 1.5
	assert worldborder damage amount of {_border} is 1.5 with "Failed to set worldborder damage amount to a float"
	set worldborder damage amount of {_border} to {_None}
	assert worldborder damage amount of {_border} is 1.5 with "Setting worldborder damage amount to None changed the damage amount"
	catch runtime errors:
		set worldborder damage amount of {_border} to NaN value
	assert last caught runtime errors contains "NaN is not a valid world border damage amount" with "NaN damage value did not throw error"
	assert worldborder damage amount of {_border} is 1.5 with "Setting worldborder damage amount to NaN value changed the damage amount"
	catch runtime errors:
		set worldborder damage amount of {_border} to infinity value
	assert last caught runtime errors contains "World border damage amount cannot be infinite" with "Infinity damage value did not throw error"
	assert worldborder damage amount of {_border} is 1.5 with "Setting worldborder damage amount to infinity changed the damage amount"
	catch runtime errors:
		set worldborder damage amount of {_border} to -infinity value
	assert last caught runtime errors contains "World border damage amount cannot be infinite" with "Negative infinity damage value did not throw error"
	assert worldborder damage amount of {_border} is 1.5 with "Setting worldborder damage amount to -infinity changed the damage amount"

	# add tests
	add 0.5 to worldborder damage amount of {_border}
	assert worldborder damage amount of {_border} is 2 with "Failed adding float to damage amount"
	add 1 to worldborder damage amount of {_border}
	assert worldborder damage amount of {_border} is 3 with "Failed adding integer to damage amount"
	add -0.5 to worldborder damage amount of {_border}
	assert worldborder damage amount of {_border} is 2.5 with "Failed adding negative float to damage amount"
	add -1 to worldborder damage amount of {_border}
	assert worldborder damage amount of {_border} is 1.5 with "Failed adding negative integer to damage amount"
	add {_None} to worldborder damage amount of {_border}
	assert worldborder damage amount of {_border} is 1.5 with "Adding None to worldborder damage amount changed the damage amount"
	catch runtime errors:
		add NaN value to worldborder damage amount of {_border}
	assert last caught runtime errors contains "NaN is not a valid world border damage amount" with "Adding NaN damage value did not throw error"
	assert worldborder damage amount of {_border} is 1.5 with "Adding NaN value to worldborder damage amount changed the damage amount"
	add infinity value to worldborder damage amount of {_border}
	assert worldborder damage amount of {_border} is 1.5 with "Adding infinity to worldborder damage amount changed the damage amount"
	add -infinity value to worldborder damage amount of {_border}
	assert worldborder damage amount of {_border} is 1.5 with "Adding -infinity to worldborder damage amount changed the damage amount"

	# remove tests
	remove 0.5 from worldborder damage amount of {_border}
	assert worldborder damage amount of {_border} is 1 with "Failed removing float from damage amount"
	remove 1 from worldborder damage amount of {_border}
	assert worldborder damage amount of {_border} is 0 with "Failed removing integer from damage amount"
	remove -0.5 from worldborder damage amount of {_border}
	assert worldborder damage amount of {_border} is 0.5 with "Failed removing negative float from damage amount"
	remove -1 from worldborder damage amount of {_border}
	assert worldborder damage amount of {_border} is 1.5 with "Failed removing negative integer from damage amount"
	remove {_None} from worldborder damage amount of {_border}
	assert worldborder damage amount of {_border} is 1.5 with "Removing None from worldborder damage amount changed the damage amount"
	remove NaN value from worldborder damage amount of {_border}
	assert worldborder damage amount of {_border} is 1.5 with "Removing NaN value from worldborder damage amount changed the damage amount"
	remove infinity value from worldborder damage amount of {_border}
	assert worldborder damage amount of {_border} is 1.5 with "Removing infinity from worldborder damage amount changed the damage amount"
	remove -infinity value from worldborder damage amount of {_border}
	assert worldborder damage amount of {_border} is 1.5 with "Removing -infinity from worldborder damage amount changed the damage amount"

	set worldborder damage amount of {_border} to -10
	assert worldborder damage amount of {_border} is 0 with "Damage amount should was set to a number less than 0"
	remove 1 from worldborder damage amount of {_border}
	assert worldborder damage amount of {_border} is 0 with "Damage amount went below 0 when subtracting"
	add -1 to worldborder damage amount of {_border}
	assert worldborder damage amount of {_border} is 0 with "Damage amount went below 0 when adding"

	reset worldborder damage amount of {_border}
	assert worldborder damage amount of {_border} is 0.2 with "Failed to reset border damage amount"
