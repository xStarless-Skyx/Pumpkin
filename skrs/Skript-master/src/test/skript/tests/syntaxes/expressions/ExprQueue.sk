using queues

test "queue creation":
	set {_queue} to a queue
	assert {_queue} exists with "queue was not created"

	delete {_queue}
	set {_queue} to a queue of "hello" and "there"
	assert {_queue} exists with "queue was not created"

test "queue polling behaviour":
	set {_queue} to a queue of "hello" and "there"

	set {_word} to the first element in {_queue}
	assert {_word} is "hello" with "element not polled"

	set {_word} to the first element in {_queue}
	assert {_word} is "there" with "element not polled"

	set {_queue} to a queue of "hello" and "there"

	set {_word} to the last element in {_queue}
	assert {_word} is "there" with "element not polled"
	set {_word} to the first element in {_queue}
	assert {_word} is "hello" with "element not polled"

	assert {_queue} is empty with "queue was not empty"

test "queue start/end":
	set {_queue} to a queue of "hello" and "there"

	set {_word} to the start of {_queue}
	assert {_word} is "hello" with "element not found"

	delete {_word}
	set {_word} to the start of {_queue} # shouldn't remove
	assert {_word} is "hello" with "element not found"

	add "foo" to the start of {_queue}

	set {_words::*} to the last 3 elements in {_queue}
	assert {_words::*} are "foo", "hello" and "there" with "elements not polled"

	assert {_queue} is empty with "queue was not empty"

test "queue emptiness":
	set {_queue} to a queue
	assert {_queue} is empty with "queue was not empty"

	set {_queue} to a queue of "hello" and "there"
	assert {_queue} is not empty with "queue was empty"

	set {_queue} to a queue
	add "hello" to {_queue}
	assert {_queue} is not empty with "queue was empty"
	set {_var} to the first element in {_queue}
	assert {_queue} is empty with "queue was not empty"

test "dequeue queue":

	set {_queue} to a queue of "hello" and "there"
	assert {_queue} is not empty with "queue was empty"

	set {_words::*} to dequeued {_queue}
	assert {_words::*} are "hello" and "there" with "elements not dequeued"
	delete {_words::*}

	set {_words::*} to the first 2 elements in {_queue}
	assert {_words::*} are "hello" and "there" with "elements not polled"

	assert {_queue} is empty with "queue was not empty"

test "loop queue":
	set {_queue} to a queue of "hello" and "there"
	set {_count} to 0

	for each {_word} in {_queue}:
		add 1 to {_count}
		if {_count} is less than 10:
			add {_word} to {_queue}

	assert {_count} is 11
