test "worldborder warning distance":
	set {_max} to max integer value
	set {_border} to worldborder of world("world")
	reset worldborder of world("world")
	assert worldborder warning distance of {_border} is 5 with "Default worldborder warning distance is not 5"

	# set tests
	set worldborder warning distance of {_border} to 1
	assert worldborder warning distance of {_border} is 1 with "Failed to set worldborder warning distance"
	set worldborder warning distance of {_border} to {_None}
	assert worldborder warning distance of {_border} is 1 with "Setting worldborder warning distance to None changed the warning distance"
	set worldborder warning distance of {_border} to NaN value
	assert worldborder warning distance of {_border} is 1 with "Setting worldborder warning distance to NaN changed the warning distance"
	set worldborder warning distance of {_border} to infinity value
	assert worldborder warning distance of {_border} is {_max} with "Failed to set worldborder warning distance to infinity"
	set worldborder warning distance of {_border} to -infinity value
	assert worldborder warning distance of {_border} is 0 with "Setting worldborder warning distance to -infinity did not stop at 0"
	set worldborder warning distance of {_border} to 1

	# add tests
	add 1 to worldborder warning distance of {_border}
	assert worldborder warning distance of {_border} is 2 with "Failed adding to warning distance"
	add -1 to worldborder warning distance of {_border}
	assert worldborder warning distance of {_border} is 1 with "Failed adding negative integer to warning distance"
	add {_None} to worldborder warning distance of {_border}
	assert worldborder warning distance of {_border} is 1 with "Adding None to worldborder warning distance changed the warning distance"
	add NaN value to worldborder warning distance of {_border}
	assert worldborder warning distance of {_border} is 1 with "Adding NaN to worldborder warning distance changed the warning distance"
	add -infinity value to worldborder warning distance of {_border}
	assert worldborder warning distance of {_border} is 0 with "Adding -infinity to worldborder warning distance did not stop at 0"
	add infinity value to worldborder warning distance of {_border}
	assert worldborder warning distance of {_border} is {_max} with "Failed adding infinity to warning distance"
	set worldborder warning distance of {_border} to 1

	# remove tests
	remove 1 from worldborder warning distance of {_border}
	assert worldborder warning distance of {_border} is 0 with "Failed removing from warning distance"
	remove -1 from worldborder warning distance of {_border}
	assert worldborder warning distance of {_border} is 1 with "Failed removing negative integer from warning distance"
	remove {_None} from worldborder warning distance of {_border}
	assert worldborder warning distance of {_border} is 1 with "Removing None from worldborder warning distance changed the warning distance"
	remove NaN value from worldborder warning distance of {_border}
	assert worldborder warning distance of {_border} is 1 with "Removing NaN from worldborder warning distance changed the warning distance"
	remove infinity value from worldborder warning distance of {_border}
	assert worldborder warning distance of {_border} is 0 with "Removing infinity from worldborder warning distance did not stop at 0"
	remove -infinity value from worldborder warning distance of {_border}
	assert worldborder warning distance of {_border} is {_max} with "Failed removing -infinity from warning distance"

	set worldborder warning distance of {_border} to -10
	assert worldborder warning distance of {_border} is 0 with "Warning distance was set to an integer less than 0"
	remove 1 from worldborder warning distance of {_border}
	assert worldborder warning distance of {_border} is 0 with "Warning distance went below 0 when subtracting"
	add -1 to worldborder warning distance of {_border}
	assert worldborder warning distance of {_border} is 0 with "Warning distance went below 0 when adding"

	reset worldborder warning distance of {_border}
	assert worldborder warning distance of {_border} is 5 with "Failed to reset border warning distance"
